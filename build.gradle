import java.nio.charset.StandardCharsets

plugins {
    id 'com.gradle.plugin-publish' version "${gradlePublishPluginVersion}"
    id 'net.researchgate.release' version "${researchgateReleaseGradlePluginVersion}"
    id "org.gradlex.reproducible-builds" version "${reproducibleBuildsPluginVersion}"
    id 'groovy'
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN // or .ALL
}

description = """Gosu language compiler for Gradle.  Built with Gradle $gradleVersion.

This has also been tested with Gradle version(s): $project.testedVersions

Projects applying this plugin require a compile-time dependency on the gosu-core-api library.  

This version requires $gosuVersion or greater. 

Please include this in the dependencies closure:

dependencies {
    implementation group: 'org.gosu-lang.gosu', name: 'gosu-core-api', version: '$project.gosuVersion'
}
"""

final boolean isSnapshot = project.version.endsWith('-SNAPSHOT')
boolean publishSnapshotToGwArtifactory = project.hasProperty('publishSnapshotToGwArtifactory')

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://central.sonatype.com/repository/maven-snapshots/' //for Gosu snapshot builds
    }
}

dependencies {
    testImplementation platform("org.junit:junit-bom:${junitPlatformVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    testImplementation "junit:junit:${junitVersion}" //TODO refactor existing unit tests and get rid of junit 4
    testImplementation "org.hamcrest:hamcrest-library:${hamcrestLibraryVersion}"
    testImplementation "org.spockframework:spock-core:${spockCoreVersion}"
    testImplementation "org.assertj:assertj-core:${assertjCoreVersion}"
    testImplementation "org.opentest4j:opentest4j:${opentest4jVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of javaVersion
    }
    withSourcesJar()
    withJavadocJar()
}

gradlePlugin {
    plugins {
        gosuPlugin {
            id = 'org.gosu-lang.gosu'
            implementationClass = 'org.gosulang.gradle.GosuPlugin'
            displayName = 'Gosu Gradle Plugin'
            description = project.description
            tags.set(['gosu'])
        }
    }
}

test {
  systemProperty 'test.gradleVersion', providers.gradleProperty('gradleVersion').orElse(project.gradle.gradleVersion).get()
  systemProperty 'test.gosuVersion', providers.gradleProperty('gosuVersion').get()
  systemProperty 'test.testedVersions', providers.gradleProperty('testedVersions').get()
  useJUnitPlatform()
  testLogging {
      events 'passed', 'failed', 'skipped'
  }
  environment('JAVA_TOOL_OPTIONS', '-Duser.language=en')
}

tasks.withType(AbstractCompile).configureEach {
    options.with {
        compilerArgs += ['-Xlint:unchecked']
        deprecation = true
        encoding = StandardCharsets.UTF_8
    }
}

//constrain memory usage when running on CI server
if(System.getenv('CI') != null) {
    tasks.withType(AbstractCompile).configureEach { //covers JavaCompile and GroovyCompile
        options.fork = false
    }
    test {
        maxHeapSize = '1g'
        systemProperty 'org.gradle.testkit.debug', 'true' // prevents the testkit from spawning daemons and running out of memory
    }
}

release {
    git {
        requireBranch.set('rel/.*')
    }
    tagTemplate = 'v${version}'
}

afterReleaseBuild.dependsOn publish, publishPlugins

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = project.name
                description = project.description
                url = 'http://gosu-lang.github.io/'
                scm {
                    connection = 'scm:git:git@github.com:gosu-lang/gosu-lang.git'
                    developerConnection = 'scm:git:git@github.com:gosu-lang/gosu-lang.git'
                    url = 'git@github.com:gosu-lang/gosu-lang.git'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Kyle Moore'
                        email = 'github@kylemoore.com'
                        organization = 'gosu-lang.org'
                    }
                    developer {
                        name = 'The Gosu Team'
                        email = 'gosu.lang.team@gmail.com'
                        organization = 'Guidewire Software, Inc.'
                    }
                }
            }
        }
    }

    repositories {
        if (isSnapshot && publishSnapshotToGwArtifactory) {
            maven {
                url System.getenv('PUBLISH_TO_REPOSITORY_URL')
                credentials {
                    username System.getenv('PUBLISH_WITH_USERNAME')
                    password System.getenv('PUBLISH_WITH_PASSWORD')
                }
            }
        } else if (isSnapshot) {
            // Publish snapshots to Sonatype snapshot repository
            maven {
                name = 'centralSnapshots'
                url = 'https://central.sonatype.com/repository/maven-snapshots/'
                credentials {
                    username = System.getenv('SNAPSHOT_USER_CENTRAL')
                    password = System.getenv('SNAPSHOT_PASS_CENTRAL')
                }
            }
        }
    }
}